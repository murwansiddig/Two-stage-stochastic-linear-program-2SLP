{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# The Farmer's Problem\n",
    "[Murwan Siddig](mailto:msiddig@clemson.edu)\n",
    "\n",
    "--------------------------------------------------------------------\n",
    "\n",
    "\n",
    "### Problem statement\n",
    "\n",
    "* You can grow Wheat, Corn, or Beans on 500 acres.\n",
    "* You need 200 tons of wheat and 240 tons of corn to feed your cattle.\n",
    "* You can grow these food on your land or buy from a super farmer.\n",
    "\n",
    "* You can sell your product at the price of:\n",
    "    * Wheat: $\\$170$ per ton.\n",
    "    * Corn: $\\$150$ per ton.\n",
    "    * Beans: $\\$36$ per ton for the first $6000$ tons, and $\\$10$ per ton after that.\n",
    "* You can buy product from the super farmer at the price of:\n",
    "    * Wheat: $\\$238$ per ton.\n",
    "    * Corn: $\\$210$ per ton.    \n",
    "* Data summary:\n",
    "|                              |Wheat|Corn |Beans                        |\n",
    "|------------------------------|-----|-----|-----------------------------|\n",
    "|Yield (T/acre)                |$2.5$|$3$  |$20$                         |\n",
    "|Production Cost (\\$/per acre) |$150$|$230$|$260$                        |\n",
    "|Selling price                 |$170$|$150$|$36/10$ (before/after $6000$)|\n",
    "|Purchase price                |$238$|$210$|-                            |\n",
    "|Cattle demand                 |$200$|$240$|-                            |\n",
    "\n",
    "* Question: Suppose you have 500 acres of land for growing crops, what is your planting plan?\n",
    "\n",
    "### Deterministic Problem formulation\n",
    "\n",
    "* **Decision variables**\n",
    "    * First-stage decisions:\n",
    "        * $x_1$: acres of wheat planted\n",
    "        * $x_2$: acres of corn planted\n",
    "        * $x_3$: acres of beans planted\n",
    "        \n",
    "    * Second-stage decisions:\n",
    "        * $w_1$: tons of wheat sold at regular price\n",
    "        * $w_2$: tons of corn sold at regular price\n",
    "        * $w_3$: tons of beans sold at regular price\n",
    "        * $e$: tons of beans sold at extra price\n",
    "        * $y_1$: tons of wheat purchased\n",
    "        * $y_2$: tons of corn purchased\n",
    "\n",
    "\\begin{aligned}\n",
    "\\max_{x,w,y} \\quad & 170w_1+150w_2+36w_3+10e-(150x_1+230x_2+260x_3+238y_1+210y_2)\\\\\n",
    "\\textrm{s.t.} \\quad & x_1+x_2+x_3\\leq 500\\\\\n",
    "  & 2.5x_1+y_1-w_1 = 200\\\\\n",
    "  & 3x_2+y_2-w_2 = 240\\\\\n",
    "  & 20x_3-w_3-e = 0\\\\\n",
    "  & w_3 \\leq 6000 \\\\\n",
    "\\end{aligned}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Input data\n",
    "yield=[2.5, 3, 20]; \n",
    "cost=[150, 230, 260];\n",
    "price = [170, 150, 36];\n",
    "over_price = 10; # Only for beans\n",
    "over_size = 6000; # Only for beans\n",
    "extra_price = [238, 210]; # Only for the first two crops\n",
    "demand = [200, 240];\n",
    "land_size = 500;\n",
    "num_crops = 3;\n",
    "num_purch = 2;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Academic license - for non-commercial use only - expires 2021-05-08\n"
     ]
    }
   ],
   "source": [
    "# techincal lines\n",
    "using JuMP\n",
    "using Gurobi\n",
    "\n",
    "#create gurobi environment\n",
    "const GRB_ENV = Gurobi.Env();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Define the model\n",
    "farmer_det = Model(optimizer_with_attributes(() -> Gurobi.Optimizer(GRB_ENV), \"OutputFlag\" => 0));\n",
    "\n",
    "#Define the variables\n",
    "@variables(farmer_det,\n",
    "           begin\n",
    "              0 <= x[i=1:num_crops] <= Inf\n",
    "              0 <= w[i=1:num_crops] <= Inf\n",
    "              0 <= e                <= Inf\n",
    "              0 <= y[i=1:num_purch] <= Inf\n",
    "           end\n",
    "          );\n",
    "\n",
    "#Define the objective\n",
    "@objective(farmer_det,\n",
    "            Max,\n",
    "            sum(price[i]*w[i] for i=1:num_crops)+10e\n",
    "           -sum(cost[i]*x[i]  for i=1:num_crops)\n",
    "           -sum(extra_price[i]*y[i] for i=1:num_purch)\n",
    "          );\n",
    "\n",
    "#Add the constraints\n",
    "@constraints(farmer_det,\n",
    "             begin\n",
    "                sum(x[i] for i=1:num_crops) <= land_size\n",
    "                yield[1]*x[1]+y[1]-w[1]     == demand[1]\n",
    "                yield[2]*x[2]+y[2]-w[2]     == demand[2]\n",
    "                yield[3]*x[3]-w[3]-e        == 0\n",
    "                w[3]                        <= over_size\n",
    "             end\n",
    "          );"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Solved! Models status is: OPTIMAL === Yaay! :D\n",
      "***********************************************\n",
      "Optimal objective value = 118600.0\n",
      "Acres of [wheat, corn, beans]  planted = [120.0, 80.0, 300.0]\n",
      "Tons of [wheat, corn, beans]  sold at regular price = [100.0, 0.0, 6000.0]\n",
      "Tons of beans sold at extra price = 0.0\n",
      "Tons of [wheat, corn] purchased = [0.0, 0.0]\n"
     ]
    }
   ],
   "source": [
    "#solve the model\n",
    "optimize!(farmer_det)\n",
    "\n",
    "#check the status \n",
    "status = termination_status(farmer_det);\n",
    "\n",
    "if status != MOI.OPTIMAL\n",
    "    println(\"Models status is: \", status, \" === Oops! :/\")\n",
    "else\n",
    "    println(\"Solved! Models status is: \", status, \" === Yaay! :D\")\n",
    "    println(\"***********************************************\")\n",
    "    println(\"Optimal objective value = \", objective_value(farmer_det))   \n",
    "    println(\"Acres of [wheat, corn, beans]  planted = \", value.(x));\n",
    "    println(\"Tons of [wheat, corn, beans]  sold at regular price = \", value.(w));\n",
    "    println(\"Tons of beans sold at extra price = \", value(e));\n",
    "    println(\"Tons of [wheat, corn] purchased = \", value.(y));    \n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Stochastic Problem formulation\n",
    "\n",
    "Suppose there are 3 possible **yield** outcomes (*scenarios*), each with **probability 1/3**:\n",
    "\n",
    "|              |Wheat|Corn |Beans|\n",
    "|--------------|-----|-----|-----|\n",
    "|Good weather  |$3$  |$3.6$|$24$ |\n",
    "|Normal weather|$2.5$|$3$  |$20$ |\n",
    "|Bad weather   |$2$  |$2.4$|$16$ |\n",
    "\n",
    "\n",
    "\n",
    "* **Decision variables**\n",
    "    * First-stage decisions:\n",
    "        * $x_1$: acres of wheat planted\n",
    "        * $x_2$: acres of corn planted\n",
    "        * $x_3$: acres of beans planted\n",
    "        \n",
    "    * Second-stage decisions (local copy for each scenario s):\n",
    "        * $w_{1,s}$: tons of wheat sold at regular price, under scenario $s$\n",
    "        * $w_{2,s}$: tons of corn sold at regular price, under scenario $s$\n",
    "        * $w_{3,s}$: tons of beans sold at regular price, under scenario $s$\n",
    "        * $e_{s}$: tons of beans sold at extra price, under scenario $s$\n",
    "        * $y_{1,s}$: tons of wheat purchased, under scenario $s$\n",
    "        * $y_{2,s}$: tons of corn purchased, under scenario $s$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    "#The stochastic yield\n",
    "yield_stoch=[3 3.6 24;\n",
    "            2.5 3 20;\n",
    "            2 2.4 16]; \n",
    "\n",
    "#The number of scenarios\n",
    "num_scens = size(yield_stoch)[1];\n",
    "\n",
    "#The probabiliy of each scenario \n",
    "p = fill(1/num_scens,num_scens);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Define the model\n",
    "farmer_stoch = Model(optimizer_with_attributes(() -> Gurobi.Optimizer(GRB_ENV), \"OutputFlag\" => 0));\n",
    "\n",
    "#Define the variables\n",
    "@variables(farmer_stoch,\n",
    "           begin\n",
    "              0 <= xx[i=1:num_crops]               <= Inf\n",
    "              0 <= ww[i=1:num_crops,s=1:num_scens] <= Inf\n",
    "              0 <= ee[s=1:num_scens]               <= Inf\n",
    "              0 <= yy[i=1:num_purch,s=1:num_scens] <= Inf\n",
    "           end\n",
    "          );\n",
    "\n",
    "\n",
    "#Define the objective\n",
    "@objective(farmer_stoch,\n",
    "            Max,\n",
    "            -sum(cost[i]*xx[i]  for i=1:num_crops)    \n",
    "            +sum((\n",
    "             sum(price[i]*ww[i,s] for i=1:num_crops)\n",
    "            +10*ee[s]\n",
    "            -sum(extra_price[i]*yy[i,s] for i=1:num_purch))*p[s]\n",
    "            for s=1:num_scens)   \n",
    "          );\n",
    "\n",
    "#Add the constraints\n",
    "@constraint(farmer_stoch, sum(xx[i] for i=1:num_crops) <= land_size);\n",
    "for s=1:num_scens\n",
    "    @constraints(farmer_stoch,\n",
    "                 begin\n",
    "                    yield_stoch[s,1]*xx[1]+yy[1,s]-ww[1,s] == demand[1]\n",
    "                    yield_stoch[s,2]*xx[2]+yy[2,s]-ww[2,s] == demand[2]\n",
    "                    yield_stoch[s,3]*xx[3]-ww[3,s]-ee[s]   == 0\n",
    "                    ww[3,s]                                <= over_size\n",
    "                 end\n",
    "              );\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Solved! Models status is: OPTIMAL === Yaay! :D\n",
      "***********************************************\n",
      "Optimal objective value = 108390.0\n",
      "Acres of [wheat, corn, beans]  planted = [170.0, 80.0, 250.0]\n",
      "=========================================\n",
      "in scenario s = 1\n",
      "Tons of [wheat, corn, beans]  sold at regular price = [310.0, 48.0, 6000.0]\n",
      "Tons of beans sold at extra price = 0.0\n",
      "Tons of [wheat, corn] purchased = [0.0, 0.0]\n",
      "=========================================\n",
      "in scenario s = 2\n",
      "Tons of [wheat, corn, beans]  sold at regular price = [225.0, 0.0, 5000.0]\n",
      "Tons of beans sold at extra price = 0.0\n",
      "Tons of [wheat, corn] purchased = [0.0, 0.0]\n",
      "=========================================\n",
      "in scenario s = 3\n",
      "Tons of [wheat, corn, beans]  sold at regular price = [140.0, 0.0, 4000.0]\n",
      "Tons of beans sold at extra price = 0.0\n",
      "Tons of [wheat, corn] purchased = [0.0, 48.00000000000003]\n"
     ]
    }
   ],
   "source": [
    "#solve the model\n",
    "optimize!(farmer_stoch)\n",
    "\n",
    "#check the status \n",
    "status_stoch = termination_status(farmer_stoch);\n",
    "\n",
    "if status_stoch != MOI.OPTIMAL\n",
    "    println(\"Models status is: \", status_stoch, \" === Oops! :/\")\n",
    "else\n",
    "    println(\"Solved! Models status is: \", status_stoch, \" === Yaay! :D\")\n",
    "    println(\"***********************************************\")\n",
    "    println(\"Optimal objective value = \", objective_value(farmer_stoch))   \n",
    "    println(\"Acres of [wheat, corn, beans]  planted = \", value.(xx));\n",
    "    for s=1:num_scens\n",
    "        println(\"=========================================\")\n",
    "        println(\"in scenario s = \", s)\n",
    "        println(\"Tons of [wheat, corn, beans]  sold at regular price = \", value.(ww)[:,s]);\n",
    "        println(\"Tons of beans sold at extra price = \", value.(ee)[s]);\n",
    "        println(\"Tons of [wheat, corn] purchased = \", value.(yy)[:,s]);\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
